# ===================================================================================
#  The dlib CMake configuration file
#
#             ** File generated automatically, do not modify **
#
#  Usage from an external project:
#    In your CMakeLists.txt, add these lines:
#
#    FIND_PACKAGE(dlib REQUIRED)
#    TARGET_LINK_LIBRARIES(MY_TARGET_NAME ${dlib_LIBRARIES})
#
#    This file will define the following variables:
#      - dlib_LIBRARIES                : The list of all imported targets for dlib modules.
#      - dlib_INCLUDE_DIRS             : The dlib include directories.
#      - dlib_VERSION                  : The version of this dlib build.
#      - dlib_VERSION_MAJOR            : Major version part of this dlib revision.
#      - dlib_VERSION_MINOR            : Minor version part of this dlib revision.
#
# ===================================================================================


if("@OpenBLAS_FOUND@")
   find_package(OpenBLAS CONFIG REQUIRED)
endif()

if("@Threads_FOUND@")
   find_package(Threads REQUIRED)
endif()

if("@DLIB_LINK_WITH_SQLITE3@")
   find_package(sqlite3 CONFIG REQUIRED)
endif()
 
if("@DLIB_GIF_SUPPORT@")
   find_package(giflib CONFIG REQUIRED)
endif()
 
if("@DLIB_PNG_SUPPORT@")
   find_package(PNG CONFIG REQUIRED)
endif()

if("@DLIB_JPEG_SUPPORT@")
   find_package(JPEG CONFIG REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/dlib.cmake")

set(dlib_LIBRARIES dlib::dlib)
set(dlib_LIBS      dlib::dlib)
set(dlib_INCLUDE_DIRS "@CMAKE_INSTALL_FULL_INCLUDEDIR@" "@dlib_needed_includes@")

mark_as_advanced(dlib_LIBRARIES)
mark_as_advanced(dlib_LIBS)
mark_as_advanced(dlib_INCLUDE_DIRS)

# Mark these variables above as deprecated.
function(__deprecated_var var access)
   if(access STREQUAL "READ_ACCESS")
      message(WARNING "The variable '${var}' is deprecated!  Instead, simply use target_link_libraries(your_app dlib::dlib).  See http://dlib.net/examples/CMakeLists.txt.html for an example.")
   endif()
endfunction()
variable_watch(dlib_LIBRARIES __deprecated_var)
variable_watch(dlib_LIBS __deprecated_var)
variable_watch(dlib_INCLUDE_DIRS __deprecated_var)



